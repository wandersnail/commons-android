apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly "com.squareup.retrofit2:retrofit:$retrofit2_version"
    compileOnly "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    compileOnly "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    compileOnly "com.alibaba:fastjson:$fastjson_version"
}

sourceCompatibility = "8"
targetCompatibility = "8"

def versionName = '1.0.3'
group = "com.github.wandersnail"
version = "$versionName"

publish {
    repoName="java"//仓库名
    userOrg = 'wandersnail' //bintray.com用户名  
    groupId = 'com.github.wandersnail' //jcenter上的路径  
    artifactId = 'commons-java' //项目名称  
    publishVersion = "$versionName" //版本号  
    desc = 'widely used tools'
    website = 'https://github.com/wandersnail'
}

if (project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}) {
    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":$project.name:generatePomFileForMavenPublication"
        }.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name "commons-java"
                    artifactId 'commons-java'
                    packaging 'jar'
                    description 'widely used tools'
                    url 'https://github.com/wandersnail'
                    version "$versionName"

                    scm {
                        url 'https://github.com/wandersnail/commons'
                        connection 'scm:git:https://github.com/wandersnail/commons.git'
                        developerConnection 'scm:git:https://github.com/wandersnail/commons.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'wandersnail'
                            name 'Zeng Fansheng'
                            email 'bingmo977@qq.com'
                        }
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
}